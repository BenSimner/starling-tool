/// The Z3 backend driver.
module Starling.Z3.Backend

open Microsoft
open Chessie.ErrorHandling
open Starling
open Starling.Utils

(*
 * Request and response types
 *)

/// Type of requests to the Z3 backend.
type Request =
    /// Only translate the term views; return `Output.Translate`.
    | Translate
    /// Translate and combine term Z3 expressions; return `Output.Combine`.
    | Combine
    /// Translate, combine, and run term Z3 expressions; return `Output.Sat`.
    | Sat

/// Type of responses from the Starling frontend.
[<NoComparison>]
type Response =
    /// Output of the term translation step only.
    | Translate of ZTerm list
    /// Output of the final Z3 terms only.
    | Combine of Microsoft.Z3.BoolExpr list
    /// Output of satisfiability reports for the Z3 terms.
    | Sat of Microsoft.Z3.Status list

(*
 * Error types
 *)

/// Type of errors generated by the Z3 backend.
type Error =
    /// A translation error occurred, given as a Translator.Error.
    | Translator of Starling.Errors.Z3.Translator.Error

(*
 * Pretty-printing
 *)

/// Pretty-prints a response.
let printResponse =
    function
    | Response.Translate t -> Starling.Pretty.Misc.printZTerms t
    | Response.Combine z -> Starling.Pretty.Misc.printZ3Exps z
    | Response.Sat s -> Starling.Pretty.Misc.printSats s

/// Pretty-prints an error.
let printError =
    function
    | Error.Translator e -> Starling.Pretty.Errors.printZ3TranslatorError e

(*
 * Driver functions
 *)

/// Shorthand for the parser stage of the frontend pipeline.
let translate ctx model = Translator.reifyZ3 ctx model >> mapMessages Error.Translator
/// Shorthand for the collation stage of the frontend pipeline.
let combine = Translator.combineTerms >> lift
/// Shorthand for the modelling stage of the frontend pipeline.
let sat = Run.run >> lift

/// Runs the Starling Z3 backend with the given context.
let runCtx ctx model =
    function
    | Request.Translate -> translate ctx model >> lift Response.Translate
    | Request.Combine -> translate ctx model >> combine ctx >> lift Response.Combine
    | Request.Sat -> translate ctx model >> combine ctx >> sat ctx >> lift Response.Sat

/// Runs the Starling Z3 backend.
/// Takes three arguments: the first is the Z3 model; the second is the
/// `Response` telling the backend what to output; and the third is the list of
/// reified terms to process with Z3.
let run model resp ts =
    use ctx = new Z3.Context()
    runCtx ctx model resp ts
